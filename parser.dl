/**
 * Identifiers
 */

.type Identifier <: symbol

.type IdentifierList = [
    head: Identifier,
    tail: IdentifierList
]

.type IdentifierOptional = [
    id: Identifier
]

/**
 * Compilation unit
 */

.type VariableDeclaration = VariableDeclaration {}

.type Dimension = Dimension { annotations: AnnotationList }

.type DimensionList = [
    key: Dimension,
    value: DimensionList
]

.type VariableDeclarationList = [
    key: VariableDeclaration,
    value: VariableDeclarationList
]

.type PackageDeclaration =
    PackageDeclaration { modifiers: AnnotationList, ids: IdentifierList }

.type ModuleDirective = ModuleDirective {}

.type ImportDeclaration =
      Import { s: IdentifierList }
    | ImportOnDemandFrom { s: IdentifierList }

.type ImportDeclarationList = [
    key: ImportDeclaration,
    value: ImportDeclarationList
]

.type CompilationUnit =
    OrdinaryCompilationUnit {
        package: PackageDeclaration,
        imports: ImportDeclarationList,
        declarations: DeclarationList
    }
    | ModularCompilationUnit {}

/**
 * Annotations
 */

.type ElementValue = ElementValueExpression {e: Expression}
                   | ElementValueAnnotation {a: Annotation}
// TODO ElementValueArrayInitializer                   

.type ElementValuePair = [
    key: Identifier,
    value: ElementValue
]

.type ElementValuePairList = [
    head: ElementValuePair,
    tail: ElementValuePairList
]

.type Annotation = [
    name: IdentifierList,
    pairs: ElementValuePairList
]

.type AnnotationList = [
    name: Annotation,
    pairs: AnnotationList
]

/**
 * Modifiers
 */

.type Modifier =
      Public {}
    | Private {}
    | Protected {}
    | Static {}
    | Native {}
    | Synchronized {}
    | Abstract {}
    | Strictfp {}
    | Final {}

.type ModifierList = [
    key: Modifier,
    value: ModifierList
]

/**
 * Expressions
 */

.type Expression =
      Add                   {e1: Expression, e2: Expression}
    | And                   {e1: Expression, e2: Expression}
    | ArrayInitializer      {e: ExpressionList}
    | Assign                {e1: Expression, s: symbol, e2: Expression}
    | BitwiseAnd            {e1: Expression, e2: Expression}
    | BitwiseExclOr         {e1: Expression, e2: Expression}
    | BitwiseInclOr         {e1: Expression, e2: Expression}
    | BitwiseNot            {e: Expression}
    | Div                   {e1: Expression, e2: Expression}
    | Equal                 {e1: Expression, e2: Expression}
    | Identifier            {id: Identifier}
    | Lambda                {p: LambdaParameters, b: LambdaBody}
    | LessThan              {e1: Expression, e2: Expression}
    | Literal               {}
    | MethodInvocation      {id: Identifier, e: ExpressionList}
    | Mod                   {e1: Expression, e2: Expression}
    | Mul                   {e1: Expression, e2: Expression}
    | Not                   {e: Expression}
    | NotEqual              {e1: Expression, e2: Expression}
    | Number                {n: number}
    | Or                    {e1: Expression, e2: Expression}
    | PostDecrement         {e: Expression}
    | PostIncrement         {e: Expression}
    | PreDecrement          {e: Expression}
    | PreIncrement          {e: Expression}
    | ShiftLeft             {e1: Expression, e2: Expression}
    | ShiftRight            {e1: Expression, e2: Expression}
    | ShiftRightUnsigned    {e1: Expression, e2: Expression}
    | Sub                   {e1: Expression, e2: Expression}
    | SwitchExpr            {e: Expression, s: SwitchRuleList}
    | This                  {}

.type ExpressionOptional = [
    expr: Expression
]

.type ExpressionList = [
    head: Expression,
    tail: ExpressionList
]

.type LambdaBody =
      LambdaBodyExpression {e: Expression}
    | LambdaBodyBlock      {e: Statement}

.type LambdaParameters = symbol

/**
 * Statements
 */

.type Statement =
      Assert                {e: Expression}
    | AssertDetailed        {e1: Expression, e2: Expression}
    | Block                 {s: StatementList}
    | Do                    {e: Expression, s: Statement}
    | Empty                 {}
    | For                   {init: ForInitOptional,
                             cond: ExpressionOptional,
                             upd: ForUpdateOptional,
                             s: Statement}
    | Label                 {id: Identifier, s: Statement}
    | IfThen                {e: Expression, s: Statement}
    | IfThenElse            {e: Expression, s1: Statement, s2: Statement}
    | Continue              {e: IdentifierOptional}
    | Break                 {e: IdentifierOptional}
    | Return                {e: ExpressionOptional}
    | Throw                 {e: Expression}
    | Switch                {e: Expression, s: SwitchRuleList}
    | SynchronizedStatement {e: Expression, s: Statement}
    | While                 {e: Expression, s: Statement}
    | ExpressionStatement   {e: Expression}
    | Yield                 {e: Expression}

.type StatementList = [
    head: Statement,
    tail: StatementList
]

.type ForInit = ForInit {}
.type ForInitOptional = [
    e: ForInit
]

.type CatchFormalParameter = CatchFormalParameter {}
.type CatchClause = CatchClause { param: CatchFormalParameter, b: Statement }

.type ForUpdate = ForUpdate {}
.type ForUpdateOptional = [
    e: ForUpdate
]

.type SwitchLabel =
      SwitchCase {e: ExpressionList}
    | SwitchDefault {}

.type SwitchRule =
      SwitchRuleExpression {label: SwitchLabel, s: Expression}
    | SwitchRuleBlock      {label: SwitchLabel, s: Statement}
    | SwitchRuleThrow      {label: SwitchLabel, s: Statement}

.type SwitchRuleList = [
    head: SwitchRule,
    tail: SwitchRuleList
]

/**
 * Types
 */

.type Type =
      TypeParameter {}
    | Boolean {}
    | Byte {}
    | Short {}
    | Int {}
    | Long {}
    | Var {}
    | Void {}
    | Variable { id: Identifier }
    | Char {}
    | Float {}
    | Array { type: Type, dims: DimensionList }
    | Double {}
    | ClassType {}

.type TypeOptional = [
    t: Type
]
         
.type TypeList = [
    head: Type,
    tail: TypeList
]

/**
 * These are constructs that may occur inside a ClassBody
 */
.type Declaration =
      ClassDeclaration {
        modifiers:  ModifierList,
        id:         Identifier,
        superclass: TypeOptional,
        body:       DeclarationList
      }
    | InterfaceDeclaration {
        modifiers:  ModifierList,
        id:         Identifier
      }
    | InstanceInitializerDeclaration {
        statement:  Statement
      }
    | MethodDeclaration {
        modifiers:  ModifierList,
        header:     MethodHeader,
        body:       Statement
      }
    | EmptyDeclaration {}

.type MethodHeader = MethodHeader {
    result: Type
}

.type DeclarationList = [
    head: Declaration,
    tail: DeclarationList
]

/**
 * Inputs
 **************************************************************************/

.input token
.decl token(s: symbol, c: Identifier, n: number)

/**
 * Tokens
 **************************************************************************/

.decl token_catch(a: number, b: number)
      token_catch(A, A + 1) :- token("TOKEN_KEYWORD", "catch", A).
.decl token_for(a: number, b: number)
      token_for(A, A + 1) :- token("TOKEN_KEYWORD", "for", A).
.decl token_semicolon(a: number, b: number)
      token_semicolon(A, A + 1) :- token("TOKEN_SEPARATOR", ";", A).
.decl token_finally(a: number, b: number)
      token_finally(A, A + 1) :- token("TOKEN_KEYWORD", "finally", A).
.decl token_byte(a: number, b: number)
      token_byte(A, A + 1) :- token("TOKEN_KEYWORD", "byte", A).
.decl token_long(a: number, b: number)
      token_long(A, A + 1) :- token("TOKEN_KEYWORD", "long", A).
.decl token_short(a: number, b: number)
      token_short(A, A + 1) :- token("TOKEN_KEYWORD", "short", A).
.decl token_int(a: number, b: number)
      token_int(A, A + 1) :- token("TOKEN_KEYWORD", "int", A).
.decl token_public(a: number, b: number)
      token_public(A, A + 1) :- token("TOKEN_KEYWORD", "public", A).
.decl token_dot(a: number, b: number)
      token_dot(A, A + 1) :- token("TOKEN_SEPARATOR", ".", A).
.decl token_package(a: number, b: number)
      token_package(A, A + 1) :- token("TOKEN_KEYWORD", "package", A).
.decl token_identifier(id: Identifier, a: number, b: number)
      token_identifier(id, A, A + 1) :- token("TOKEN_IDENTIFIER", id, A).
.decl token_left_paren(a: number, b: number)
      token_left_paren(A, A + 1) :- token("TOKEN_LEFT_PAREN", "(", A).
.decl token_right_paren(a: number, b: number)
      token_right_paren(A, A + 1) :- token("TOKEN_RIGHT_PAREN", ")", A).
.decl token_left_square_bracket(a: number, b: number)
      token_left_square_bracket(A, A + 1) :- token("TOKEN_BRACKET", "[", A).
.decl token_right_square_bracket(a: number, b: number)
      token_right_square_bracket(A, A + 1) :- token("TOKEN_BRACKET", "]", A).
.decl token_left_curly_bracket(a: number, b: number)
      token_left_curly_bracket(A, A + 1) :- token("TOKEN_BRACKET", "{", A).
.decl token_right_curly_bracket(a: number, b: number)
      token_right_curly_bracket(A, A + 1) :- token("TOKEN_BRACKET", "}", A).
.decl token_less_than(a: number, b: number)
      token_less_than(A, A + 1) :- token("TOKEN_OPERATOR", "<", A).
.decl token_greater_than(a: number, b: number)
      token_greater_than(A, A + 1) :- token("TOKEN_OPERATOR", "<", A).
.decl token_asterisk(a: number, b: number)
      token_asterisk(A, A + 1) :- token("TOKEN_OPERATOR", "*", A).
.decl token_import(a: number, b: number)
      token_import(A, A + 1) :- token("TOKEN_KEYWORD", "import", A).
.decl token_comma(a: number, b: number)
      token_comma(A, A + 1) :- token("TOKEN_SEPARATOR", ",", A).
.decl token_class(a: number, b: number)
      token_class(A, A + 1) :- token("TOKEN_KEYWORD", "class", A).
.decl token_extends(a: number, b: number)
      token_extends(A, A + 1) :- token("TOKEN_KEYWORD", "extends", A).
.decl token_float(a: number, b: number)
      token_float(A, A + 1) :- token("TOKEN_KEYWORD", "float", A).
.decl token_double(a: number, b: number)
      token_double(A, A + 1) :- token("TOKEN_KEYWORD", "double", A).
.decl token_boolean(a: number, b: number)
      token_boolean(A, A + 1) :- token("TOKEN_KEYWORD", "boolean", A).
.decl token_return(a: number, b: number)
      token_return(A, A + 1) :- token("TOKEN_KEYWORD", "return", A).
.decl token_switch(a: number, b: number)
      token_switch(A, A + 1) :- token("TOKEN_KEYWORD", "switch", A).
.decl token_colon(a: number, b: number)
      token_colon(A, A + 1) :- token("TOKEN_SEPARATOR", ":", A).
.decl token_var(a: number, b: number)
      token_var(A, A + 1) :- token("TOKEN_KEYWORD", "var", A).
.decl token_do(a: number, b: number)
      token_do(A, A + 1) :- token("TOKEN_KEYWORD", "do", A).
.decl token_while(a: number, b: number)
      token_while(A, A + 1) :- token("TOKEN_KEYWORD", "while", A).
.decl token_continue(a: number, b: number)
      token_continue(A, A + 1) :- token("TOKEN_KEYWORD", "continue", A).
.decl token_synchronized(a: number, b: number)
      token_synchronized(A, A + 1) :- token("TOKEN_KEYWORD", "synchronized", A).
.decl token_native(a: number, b: number)
      token_native(A, A + 1) :- token("TOKEN_KEYWORD", "native", A).
.decl token_break(a: number, b: number)
      token_break(A, A + 1) :- token("TOKEN_KEYWORD", "break", A).
.decl token_case(a: number, b: number)
      token_case(A, A + 1) :- token("TOKEN_KEYWORD", "case", A).
.decl token_yield(a: number, b: number)
      token_yield(A, A + 1) :- token("TOKEN_KEYWORD", "yield", A).
.decl token_throw(a: number, b: number)
      token_throw(A, A + 1) :- token("TOKEN_KEYWORD", "throw", A).
.decl token_arrow(a: number, b: number)
      token_arrow(A, A + 1) :- token("TOKEN_SEPARATOR", "->", A).
.decl token_if(a: number, b: number)
      token_if(A, A + 1) :- token("TOKEN_KEYWORD", "if", A).
.decl token_else(a: number, b: number)
      token_else(A, A + 1) :- token("TOKEN_KEYWORD", "else", A).
.decl token_assert(a: number, b: number)
      token_assert(A, A + 1) :- token("TOKEN_KEYWORD", "assert", A).
.decl token_static(a: number, b: number)
      token_static(A, A + 1) :- token("TOKEN_KEYWORD", "static", A).
.decl token_question_mark(a: number, b: number)
      token_question_mark(A, A + 1) :- token("TOKEN_SEPARATOR", "?", A).
.decl token_at(a: number, b: number)
      token_at(A, A + 1) :- token("TOKEN_SEPARATOR", "@", A).
.decl token_this(a: number, b: number)
      token_this(A, A + 1) :- token("TOKEN_KEYWORD", "this", A).
.decl token_void(a: number, b: number)
      token_void(A, A + 1) :- token("TOKEN_KEYWORD", "void", A).
.decl token_strictfp(a: number, b: number)
      token_strictfp(A, A + 1) :- token("TOKEN_KEYWORD", "strictfp", A).
.decl token_default(a: number, b: number)
      token_default(A, A + 1) :- token("TOKEN_KEYWORD", "default", A).
.decl token_private(a: number, b: number)
      token_private(A, A + 1) :- token("TOKEN_KEYWORD", "private", A).
.decl token_final(a: number, b: number)
      token_final(A, A + 1) :- token("TOKEN_KEYWORD", "final", A).
.decl token_abstract(a: number, b: number)
      token_abstract(A, A + 1) :- token("TOKEN_KEYWORD", "abstract", A).
.decl token_protected(a: number, b: number)
      token_protected(A, A + 1) :- token("TOKEN_KEYWORD", "protected", A).
.decl token_char(a: number, b: number)
      token_char(A, A + 1) :- token("TOKEN_KEYWORD", "char", A).

/**
 * Helper functions
 **************************************************************************/

.decl any_position(n: number)
any_position(n) :- token(_, _, n).

#define ZERO_OR_MORE(take_many, take_one) \
    take_many(nil, A, A) :- any_position(A). \
    take_many([x, xs], A, C) :- take_one(x, A, B), take_many(xs, B, C).

#define OPTIONAL(take_zero_or_one, take_one) \
    take_zero_or_one(nil, A, A) :- any_position(A). \
    take_zero_or_one([x], A, B) :- take_one(x, A, B).

#define INFIX(node, current_level, next_level, symbol) \
    current_level(node(e1, e2), A, C) :- \
        current_level(e1, A, B), B > A, \
        token("TOKEN_OPERATOR", symbol, B), \
        next_level(e2, B + 1, C), C > B + 1.

#define PREFIX(node, current_level, next_level, symbol) \
    current_level(node(e1), A, B) :- \
        token("TOKEN_OPERATOR", symbol, A), \
        next_level(e1, A + 1, B), B > A + 1.

#define POSTFIX(node, current_level, next_level, symbol) \
    current_level(node(e1), A, B + 1) :- \
        next_level(e1, A, B), B > A, \
        token("TOKEN_OPERATOR", symbol, B).

/**
 * Lexical structure
 *
 * Adapted from §3 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl type_identifier(s: Identifier, a: number, b: number)
type_identifier(t, A, B) :- token_identifier(t, A, B).
// TODO not var or yield

.decl identifier(s: Identifier, a: number, b: number)
identifier(t, A, B) :- token_identifier(t, A, B).

.decl identifier?(s: IdentifierOptional, a: number, b: number)
OPTIONAL(identifier?, identifier)

.decl literal(a: number, b: number)

/**
 * Types, Values and Variables
 *
 * Adapted from §4 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl type(s: Type, a: number, b: number)

.decl primitive_type(s: Type, a: number, b: number)

.decl numeric_type(s: Type, a: number, b: number)
numeric_type(e, A, B) :- integral_type(e, A, B).
numeric_type(e, A, B) :- floating_point_type(e, A, B).

.decl integral_type(t: Type, a: number, b: number)
integral_type($Byte,  A, B) :- token_byte(A, B).
integral_type($Short, A, B) :- token_short(A, B).
integral_type($Int,   A, B) :- token_int(A, B).
integral_type($Long,  A, B) :- token_long(A, B).
integral_type($Char,  A, B) :- token_char(A, B).

.decl floating_point_type(t: Type, a: number, b: number)
floating_point_type($Float,  A, B) :- token_float(A, B).
floating_point_type($Double, A, B) :- token_double(A, B).

.decl reference_type(t: Type, a: number, b: number)
reference_type(t, A, B) :- class_or_interface_type(t, A, B).
reference_type(t, A, B) :- type_variable(t, A, B).
reference_type(t, A, B) :- array_type(t, A, B).

.decl class_or_interface_type(t: Type, a: number, b: number)
class_or_interface_type(t, A, B) :- class_type(t, A, B).
class_or_interface_type(t, A, B) :- interface_type(t, A, B).

.decl class_type(t: Type, a: number, b: number)

.decl interface_type(t: Type, a: number, b: number)
interface_type(t, A, B) :- class_type(t, A, B).

.decl type_variable(t: Type, a: number, b: number)

.decl array_type(t: Type, a: number, b: number)

.decl dims(e: DimensionList, a: number, b: number)
dims([x, nil], A, B) :- dim(x, A, B).
dims([x, xs],  A, B) :- dim(x, A, B), dims(xs, A, B).

.decl dim(t: Dimension, a: number, b: number)
dim($Dimension(a), A, D) :-
    annotations?(a, A, B),
    token_left_square_bracket(B, C),
    token_right_square_bracket(C, D).

.decl type_parameter(t: Type, a: number, b: number)

.decl type_parameter_modifier(t: Annotation, a: number, b: number)
type_parameter_modifier(t, A, B) :- annotation(t, A, B).

.decl type_bound(a: number, b: number)

.decl additional_bound(a: number, b: number)

.decl type_arguments(a: number, b: number)

.decl type_argument_list(a: number, b: number)

.decl type_argument(a: number, b: number)

.decl wildcard(a: number, b: number)

.decl wildcard_bounds(a: number, b: number)

/**
 * Names
 *
 * Adapted from §6 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl module_name(s: IdentifierList, a: number, b: number)

.decl package_name(s: IdentifierList, a: number, b: number)

.decl type_name(s: IdentifierList, a: number, b: number)
type_name([t, nil], A, B) :- type_identifier(t, A, B).
type_name([t, tail], A, D) :-
    package_or_type_name(tail, A, B),
    token_dot(B, C),
    type_identifier(t, C, D).
// ... reverse?

.decl expression_name(s: IdentifierList, a: number, b: number)
expression_name([id, nil], A, B) :- token_identifier(id, A, B).
expression_name([id, tail], A, D) :-
    ambiguous_name(tail, A, B),
    token_dot(B, C),
    token_identifier(id, C, D).
// ... reverse?

.decl method_name(x: Identifier, a: number, b: number)
method_name(id, A, B) :- token_identifier(id, A, B).
// ... TODO: not yield

.decl package_or_type_name(s: IdentifierList, a: number, b: number)
package_or_type_name([x, nil], A, B) :- token_identifier(x, A, B).
package_or_type_name([x, xs], A, D) :-
    package_or_type_name(xs, A, B),
    token_dot(B, C),
    token_identifier(x, C, D).
// ... reverse?

.decl ambiguous_name(s: IdentifierList, a: number, b: number)
ambiguous_name([x, nil], A, B) :- token_identifier(x, A, B).
ambiguous_name([x, xs], A, D) :-
    ambiguous_name(xs, A, B),
    token_dot(B, C),
    token_identifier(x, C, D).
// ... reverse?

/**
 * Packages and Modules
 *
 * Adapted from §7 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl compilation_unit(e: CompilationUnit, a: number, b: number)
compilation_unit(e, A, B) :- ordinary_compilation_unit(e, A, B).
compilation_unit(e, A, B) :- modular_compilation_unit(e, A, B).

.decl ordinary_compilation_unit(e: CompilationUnit, a: number, b: number)

.decl modular_compilation_unit(e: CompilationUnit, a: number, b: number)

.decl package_declaration(e: PackageDeclaration, a: number, b: number)
package_declaration($PackageDeclaration(mods, ids), A, E) :-
    package_modifiers?(mods, A, B),
    token_package(B, C),
    package_declaration_identifiers(ids, C, D),
    token_semicolon(D, E).

.decl package_declaration_identifiers(e: IdentifierList, a: number, b: number)
package_declaration_identifiers([x, nil], A, B) :- identifier(x, A, B).
package_declaration_identifiers([x, xs], A, D) :-
    identifier(x, A, B),
    token_dot(B, C),
    package_declaration_identifiers(xs, C, D).

.decl package_modifier(e: Annotation, a: number, b: number)
package_modifier(e, A, B) :- annotation(e, A, B).

.decl package_modifiers?(e: AnnotationList, a: number, b: number)
ZERO_OR_MORE(package_modifiers?, package_modifier)

.decl import_declaration(s: ImportDeclaration, a: number, b: number)
import_declaration(e, A, B) :- single_type_import_declaration(e, A, B).
import_declaration(e, A, B) :- type_import_on_demand_declaration(e, A, B).
import_declaration(e, A, B) :- single_static_import_declaration(e, A, B).
import_declaration(e, A, B) :- static_import_on_demand_declaration(e, A, B).

.decl single_type_import_declaration(s: ImportDeclaration, a: number, b: number)
single_type_import_declaration($Import(syms), A, D) :-
    token_import(A, B),
    type_name(syms, B, C),
    token_semicolon(C, D).

.decl type_import_on_demand_declaration(s: ImportDeclaration, a: number, b: number)
type_import_on_demand_declaration($ImportOnDemandFrom(syms), A, F) :-
    token_import(A, B),
    type_name(syms, B, C),
    token_dot(C, D),
    token_asterisk(D, E),
    token_semicolon(E, F).

.decl single_static_import_declaration(s: ImportDeclaration, a: number, b: number)

.decl static_import_on_demand_declaration(s: ImportDeclaration, a: number, b: number)

.decl type_declaration(s: Declaration, a: number, b: number)
type_declaration(e, A, B) :- class_declaration(e, A, B).
type_declaration(e, A, B) :- interface_declaration(e, A, B).

.decl module_declaration(s: Declaration, a: number, b: number)

.decl module_directive(s: ModuleDirective, a: number, b: number)

.decl requires_modifier(s: Identifier, a: number, b: number)

/**
 * Classes
 *
 * Adapted from §8 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.output class_declaration
.decl class_declaration(s: Declaration, a: number, b: number)
class_declaration(e, A, B) :- normal_class_declaration(e, A, B).
class_declaration(e, A, B) :- enum_declaration(e, A, B).

.decl normal_class_declaration(s: Declaration, a: number, b: number)
normal_class_declaration($ClassDeclaration(modifiers, id, sup, declarations), A, F) :-
    class_modifiers?(modifiers, A, B),
    token_class(B, C),
    type_identifier(id, C, D),
    superclass?(sup, D, E),
    class_body(declarations, E, F).

.decl class_modifiers?(s: ModifierList, a: number, b: number)
ZERO_OR_MORE(class_modifiers?, class_modifier)

.decl class_modifier(s: Modifier, a: number, b: number)
class_modifier($Public,     A, B) :- token_public(A, B).
class_modifier($Protected,  A, B) :- token_protected(A, B).
class_modifier($Private,    A, B) :- token_private(A, B).
class_modifier($Abstract,   A, B) :- token_abstract(A, B).
class_modifier($Static,     A, B) :- token_static(A, B).
class_modifier($Final,      A, B) :- token_final(A, B).
class_modifier($Strictfp,   A, B) :- token_strictfp(A, B).

.decl type_parameters(s: TypeList, a: number, b: number)
type_parameters(nil, A, A) :- any_position(A).
type_parameters(l, A, D) :-
    token_less_than(A, B),
    type_parameter_list(l, B, C),
    token_greater_than(C, D).

.decl type_parameter_list(s: TypeList, a: number, b: number)
type_parameter_list([e, nil], A, B) :- type_parameter(e, A, B).
type_parameter_list([e, tail], A, D) :-
    type_parameter(e, A, B),
    token_comma(B, C),
    type_parameter_list(tail, C, D).

.decl superclass(t: Type, a: number, b: number)
superclass(t, A, C) :-
    token_extends(A, B),
    class_type(t, B, C).

.decl superclass?(t: TypeOptional, a: number, b: number)
OPTIONAL(superclass?, superclass)

.decl superinterfaces(t: Type, a: number, b: number)

// TODO InterfaceTypeList

.decl class_body(s: DeclarationList, a: number, b: number)
class_body(declarations, A, D) :-
    token_left_curly_bracket(A, B),
    class_body_declarations?(declarations, B, C),
    token_right_curly_bracket(C, D).

.decl class_body_declarations?(d: DeclarationList, a: number, b: number)
ZERO_OR_MORE(class_body_declarations?, class_body_declaration)

.decl class_body_declaration(d: Declaration, a: number, b: number)
class_body_declaration(e, A, B) :- class_member_declaration(e, A, B).
class_body_declaration(e, A, B) :- instance_initializer(e, A, B).
// ... TODO

.decl class_member_declaration(s: Declaration, a: number, b: number)
class_member_declaration(e, A, B) :- field_declaration(e, A, B).
class_member_declaration(e, A, B) :- method_declaration(e, A, B).
class_member_declaration(e, A, B) :- class_declaration(e, A, B).
class_member_declaration(e, A, B) :- interface_declaration(e, A, B).
class_member_declaration($EmptyDeclaration, A, B) :- token_semicolon(A, B).

.decl field_declaration(s: Declaration, a: number, b: number)

.decl field_modifier(s: Identifier, a: number, b: number)

.decl variable_declarator_list(s: VariableDeclarationList, a: number, b: number)

.decl variable_declarator(s: VariableDeclaration, a: number, b: number)

// TODO VariableDeclaratorId

.decl variable_initializer(s: Expression, a: number, b: number)
variable_initializer(e, A, B) :- expression(e, A, B).
variable_initializer(e, A, B) :- array_initializer(e, A, B).

.decl unann_type(t: Type, a: number, b: number)
unann_type(e, A, B) :- unann_primitive_type(e, A, B).
unann_type(e, A, B) :- unann_reference_type(e, A, B).

.decl unann_primitive_type(t: Type, a: number, b: number)
unann_primitive_type($Boolean, A, B) :- token_boolean(A, B).
unann_primitive_type(e, A, B) :- numeric_type(e, A, B).

.decl unann_reference_type(t: Type, a: number, b: number)
unann_reference_type(t, A, B) :- unann_class_or_interface_type(t, A, B).
unann_reference_type(t, A, B) :- unann_type_variable(t, A, B).
unann_reference_type(t, A, B) :- unann_array_type(t, A, B).

.decl unann_class_or_interface_type(t: Type, a: number, b: number)
unann_class_or_interface_type(t, A, B) :- unann_class_type(t, A, B).
unann_class_or_interface_type(t, A, B) :- unann_interface_type(t, A, B).

.decl unann_class_type(t: Type, a: number, b: number)

.decl unann_interface_type(t: Type, a: number, b: number)
unann_interface_type(t, A, B) :- unann_class_type(t, A, B).

.decl unann_type_variable(t: Type, a: number, b: number)
unann_type_variable($Variable(e), A, B) :- type_identifier(e, A, B).

.decl unann_array_type(t: Type, a: number, b: number)
unann_array_type($Array(t, d), A, C) :-
    primitive_type(t, A, B),
    dims(d, B, C).
// ...

.decl method_declaration(s: Declaration, a: number, b: number)
method_declaration($MethodDeclaration(modifiers, header, body), A, D) :-
    method_modifiers?(modifiers, A, B),
    method_header(header, B, C),
    method_body(body, C, D).

.decl method_modifiers?(s: ModifierList, a: number, b: number)
ZERO_OR_MORE(method_modifiers?, method_modifier)

.decl method_modifier(s: Modifier, a: number, b: number)
method_modifier($Public,        A, B) :- token_public(A, B).
method_modifier($Protected,     A, B) :- token_protected(A, B).
method_modifier($Private,       A, B) :- token_private(A, B).
method_modifier($Abstract,      A, B) :- token_abstract(A, B).
method_modifier($Static,        A, B) :- token_static(A, B).
method_modifier($Final,         A, B) :- token_final(A, B).
method_modifier($Synchronized,  A, B) :- token_synchronized(A, B).
method_modifier($Native,        A, B) :- token_native(A, B).
method_modifier($Strictfp,      A, B) :- token_strictfp(A, B).

.decl method_header(s: MethodHeader, a: number, b: number)

.decl result(t: Type, a: number, b: number)
result(t, A, B) :- unann_type(t, A, B).
result($Void, A, B) :- token_void(A, B).

// TODO MethodDeclarator

// TODO ReceiverParameter

// TODO FormalParameterList

// TODO FormalParameter

// TODO VariableArityParameter

// TODO VariableModifier

// TODO Throws

// TODO ExceptionTypeList

// TODO ExceptionType

.decl method_body(s: Statement, a: number, b: number)
method_body(e, A, C) :-
    block(e, A, B),
    token_semicolon(B, C).

.decl instance_initializer(s: Declaration, a: number, b: number)
instance_initializer($InstanceInitializerDeclaration(e), A, B) :- block(e, A, B).

.decl static_initializer(s: Statement, a: number, b: number)
static_initializer(e, A, C) :-
    token_static(A, B),
    block(e, B, C).
// ... TODO - looses static information

// TODO ConstructorDeclaration

// TODO ConstructorModifier

// TODO ConstructorDeclarator

.decl simple_type_name(s: symbol, l: number, n: number)
simple_type_name(e, A, B) :- type_identifier(e, A, B).

// TODO ConstructorBody

// TODO ExplicitConstructorInvocation

.decl enum_declaration(s: Declaration, a: number, b: number)

// TODO EnumBody

// TODO EnumConstantList

// TODO EnumConstant

// TODO EnumConstantModifier

// TODO EnumBodyDeclaration

/**
 * Interfaces
 *
 * Adapted from §9 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl interface_declaration(s: Declaration, a: number, b: number)
interface_declaration(e, A, B) :- normal_interface_declaration(e, A, B).
interface_declaration(e, A, B) :- annotation_type_declaration(e, A, B).

.decl normal_interface_declaration(s: Declaration, a: number, b: number)

.decl annotation_type_declaration(s: Declaration, a: number, b: number)

.decl annotation_type_body(a: number, b: number)

.decl annotation_type_member_declaration(a: number, b: number)

.decl annotation_type_element_declaration(a: number, b: number)

.decl annotation_type_element_modifier(a: number, b: number)

.decl default_value(a: number, b: number)

.decl annotation(e: Annotation, a: number, b: number)
annotation(e, A, B) :- normal_annotation(e, A, B).

.decl annotations?(e: AnnotationList, a: number, b: number)
ZERO_OR_MORE(annotations?, annotation)

.decl normal_annotation(e: Annotation, a: number, b: number)
normal_annotation([name, nil], A, E) :-
    token_at(A, B),
    type_name(name, B, C),
    token_left_paren(C, D),
    token_right_paren(D, E).
normal_annotation([name, pairs], A, F) :-
    token_at(A, B),
    type_name(name, B, C),
    token_left_paren(C, D),
    element_value_pair_list(pairs, D, E),
    token_right_paren(E, F).

.decl element_value_pair_list(e: ElementValuePairList, a: number, b: number)

.decl element_value_pair(e: ElementValuePair, a: number, b: number)

.decl element_value(e: ElementValue, a: number, b: number)

/**
 * Arrays
 *
 * Adapted from §10 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl array_initializer(e: Expression, a: number, b: number)
array_initializer($ArrayInitializer(e), A, D) :- 
    token_left_curly_bracket(A, B),
    variable_initializer_list?(e, B, C),
    token_right_curly_bracket(C, D).
array_initializer($ArrayInitializer(e), A, E) :- 
    token_left_curly_bracket(A, B),
    variable_initializer_list?(e, B, C),
    token_semicolon(C, D),
    token_right_curly_bracket(D, E).

.decl variable_initializer_list(s: ExpressionList, a: number, b: number)
variable_initializer_list([x, nil], A, A) :- variable_initializer(x, A, B).
variable_initializer_list([x, xs], A, D) :-
    variable_initializer(x, A, B),
    token_comma(B, C),
    variable_initializer_list(xs, C, D).

.decl variable_initializer_list?(s: ExpressionList, a: number, b: number)
variable_initializer_list?(nil, A, A) :- any_position(A).
variable_initializer_list?(x, A, B) :- variable_initializer_list(x, A, B).

/**
 * Blocks and Statements
 *
 * Adapted from §14 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl block(s: Statement, a: number, b: number)
block($Block(s), A, D) :-
    token_left_curly_bracket(A, B),
    block_statements?(s, B, C),
    token_left_curly_bracket(C, D).

.decl block_statements?(s: StatementList, a: number, b: number)
ZERO_OR_MORE(block_statements?, block_statement)

.decl block_statement(s: Statement, a: number, b: number)
block_statement(e, A, B) :- statement(e, A, B).

.decl local_variable_declaration_statement(s: Statement, a: number, b: number)
local_variable_declaration_statement(e, A, C) :-
    local_variable_declaration(e, A, B),
    token_semicolon(B, C).

.decl local_variable_declaration(s: Statement, a: number, b: number)

.decl local_variable_type(t: Type, a: number, b: number)
local_variable_type(t, A, B) :- unann_type(t, A, B).

.decl statement(s: Statement, a: number, b: number)
statement(e, A, B) :- statement_without_trailing_substatement(e, A, B).
statement(e, A, B) :- labeled_statement(e, A, B).
statement(e, A, B) :- if_then_statement(e, A, B).
statement(e, A, B) :- if_then_else_statement(e, A, B).
statement(e, A, B) :- while_statement(e, A, B).
statement(e, A, B) :- for_statement(e, A, B).

.decl statement_no_short_if(s: Statement, a: number, b: number)
statement_no_short_if(e, A, B) :- statement_without_trailing_substatement(e, A, B).
statement_no_short_if(e, A, B) :- labeled_statement_no_short_if(e, A, B).
statement_no_short_if(e, A, B) :- if_then_else_statement_no_short_if(e, A, B).
statement_no_short_if(e, A, B) :- while_statement_no_short_if(e, A, B).
statement_no_short_if(e, A, B) :- for_statement_no_short_if(e, A, B).

.decl statement_without_trailing_substatement(s: Statement, a: number, b: number)
statement_without_trailing_substatement(e, A, B) :- block(e, A, B).
statement_without_trailing_substatement(e, A, B) :- empty_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- expression_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- assert_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- switch_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- do_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- break_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- continue_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- return_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- synchronized_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- throw_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- try_statement(e, A, B).
statement_without_trailing_substatement(e, A, B) :- yield_statement(e, A, B).

.decl empty_statement(s: Statement, a: number, b: number)
empty_statement($Empty, A, B) :- token_semicolon(A, B).

.decl labeled_statement(s: Statement, a: number, b: number)
labeled_statement($Label(id, st), A, D) :-
    token_identifier(id, A, B),
    token_colon(B, C),
    statement(st, C, D).

.decl labeled_statement_no_short_if(s: Statement, a: number, b: number)
labeled_statement_no_short_if($Label(id, st), A, B) :-
    token_identifier(id, A, B),
    token_colon(B, C),
    statement_no_short_if(st, C, D).

.decl expression_statement(e: Statement, a: number, b: number)
expression_statement($ExpressionStatement(e), A, C) :-
    statement_expression(e, A, B),
    token_semicolon(B, C).

.decl statement_expression(e: Expression, a: number, b: number)
statement_expression(e, A, B) :- assignment(e, A, B).
statement_expression(e, A, B) :- pre_increment_expression(e, A, B).
statement_expression(e, A, B) :- pre_decrement_expression(e, A, B).
statement_expression(e, A, B) :- post_increment_expression(e, A, B).
statement_expression(e, A, B) :- post_decrement_expression(e, A, B).
statement_expression(e, A, B) :- method_invocation(e, A, B).
statement_expression(e, A, B) :- class_instance_creation_expression(e, A, B).

.decl if_then_statement(s: Statement, a: number, b: number)
if_then_statement($IfThen(e, s), A, F) :-
    token_if(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    statement(s, E, F).

.decl if_then_else_statement(s: Statement, a: number, b: number)
if_then_else_statement($IfThenElse(e, s1, s2), A, H) :-
    token_if(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    statement_no_short_if(s1, E, F),
    token_else(F, G),
    statement(s2, G, H).

.decl if_then_else_statement_no_short_if(s: Statement, a: number, b: number)
if_then_else_statement_no_short_if($IfThenElse(e, s1, s2), A, H) :-
    token_if(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    statement_no_short_if(s1, E, F),
    token_else(F, G),
    statement_no_short_if(s2, G, H).

.decl assert_statement(s: Statement, a: number, b: number)
assert_statement($Assert(e), A, D) :-
    token_assert(A, B),
    expression(e, B, C),
    token_semicolon(C, D).
assert_statement($AssertDetailed(e1, e2), A, F) :-
    token_assert(A, B),
    expression(e1, B, C),
    token_colon(C, D),
    expression(e2, D, E),
    token_semicolon(E, F).

.decl switch_statement(s: Statement, a: number, b: number)
switch_statement($Switch(e, rules), A, F) :-
    token_switch(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    switch_block(rules, E, F).

.decl switch_block(s: SwitchRuleList, a: number, b: number)
switch_block([rule, rules], A, E) :-
    token_left_curly_bracket(A, B),
    switch_rule(rule, B, C),
    switch_rules?(rules, C, D),
    token_right_curly_bracket(D, E).

.decl switch_rules?(e: SwitchRuleList, a: number, b: number)
ZERO_OR_MORE(switch_rules?, switch_rule)

.decl switch_rule(e: SwitchRule, a: number, b: number)
switch_rule($SwitchRuleExpression(label, expr), A, E) :-
    switch_label(label, A, B),
    token_arrow(B, C),
    expression(expr, C, D),
    token_semicolon(D, E).
switch_rule($SwitchRuleBlock(label, stmt), A, D) :-
    switch_label(label, A, B),
    token_arrow(B, C),
    block(stmt, C, D).
switch_rule($SwitchRuleBlock(label, stmt), A, C) :-
    switch_label(label, A, B),
    token_arrow(B, C),
    throw_statement(stmt, C, D).

.decl switch_block_statement_group(a: number, b: number)

.decl switch_label(s: SwitchLabel, a: number, b: number)
switch_label($SwitchDefault, A, B) :- token_default(A, B).
switch_label($SwitchCase(exprs), A, C) :-
    token_case(A, B),
    case_constants(exprs, B, C).

.decl case_constants(e: ExpressionList, a: number, b: number)
case_constants([x, nil], A, B) :- case_constant(x, A, B).
case_constants([x, xs], A, D) :-
    case_constant(x, A, B),
    token_comma(B, C),
    case_constants(xs, C, D).

.decl case_constant(e: Expression, a: number, b: number)
case_constant(e, A, B) :- conditional_expression(e, A, B).

.decl while_statement(s: Statement, a: number, b: number)
while_statement($While(e, s), A, F) :-
    token_while(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    statement(s, E, F).

.decl while_statement_no_short_if(s: Statement, a: number, b: number)
while_statement_no_short_if($While(e, s), A, F) :-
    token_while(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    statement_no_short_if(s, E, F).

.decl do_statement(s: Statement, a: number, b: number)
do_statement($Do(e, s), A, H) :-
    token_do(A, B),
    statement(s, B, C),
    token_while(C, D),
    token_left_paren(D, E),
    expression(e, E, F),
    token_right_paren(F, G),
    token_semicolon(G, H).

.decl for_statement(s: Statement, a: number, b: number)
for_statement(e, A, B) :- for_statement_no_short_if(e, A, B).
for_statement(e, A, B) :- enhanced_for_statement(e, A, B).

.decl for_statement_no_short_if(s: Statement, a: number, b: number)
for_statement_no_short_if(e, A, B) :- basic_for_statement_no_short_if(e, A, B).
for_statement_no_short_if(e, A, B) :- enhanced_for_statement_no_short_if(e, A, B).

.decl basic_for_statement(s: Statement, a: number, b: number)
basic_for_statement($For(init, expr, upd, stat), A, J) :-
    token_for(A, B),
    token_left_paren(B, C),
    for_init?(init, C, D),
    token_semicolon(D, E),
    expression?(expr, E, F),
    token_semicolon(F, G),
    for_update?(upd, G, H),
    token_right_paren(H, I),
    statement(stat, I, J).

.decl basic_for_statement_no_short_if(s: Statement, a: number, b: number)
basic_for_statement($For(init, expr, upd, stat), A, J) :-
    token_for(A, B),
    token_left_paren(B, C),
    for_init?(init, C, D),
    token_semicolon(D, E),
    expression?(expr, E, F),
    token_semicolon(F, G),
    for_update?(upd, G, H),
    token_right_paren(H, I),
    statement_no_short_if(stat, I, J).

.decl for_init(s: ForInit, a: number, b: number)
for_init($ForInit, A, B) :- statement_expression_list(_, A, B).
for_init($ForInit, A, B) :- local_variable_declaration(_, A, B).
.decl for_init?(s: ForInitOptional, a: number, b: number)
OPTIONAL(for_init?, for_init)

.decl for_update(s: ForUpdate, a: number, b: number)
for_update($ForUpdate, A, B) :- statement_expression_list(e, A, B).
.decl for_update?(s: ForUpdateOptional, a: number, b: number)
OPTIONAL(for_update?, for_update)

.decl statement_expression_list(s: ExpressionList, a: number, b: number)
statement_expression_list([x, nil], A, B) :- statement_expression(x, A, B).
statement_expression_list([x, xs], A, D) :-
    statement_expression(x, A, B),
    token_comma(B, C),
    statement_expression_list(xs, C, D).

.decl enhanced_for_statement(s: Statement, a: number, b: number)

.decl enhanced_for_statement_no_short_if(s: Statement, a: number, b: number)

.decl break_statement(s: Statement, a: number, b: number)
break_statement($Break(id), A, D) :-
    token_break(A, B),
    identifier?(id, B, C),
    token_semicolon(C, D).

.decl yield_statement(s: Statement, a: number, b: number)
yield_statement($Yield(e), A, D) :-
    token_yield(A, B),
    expression(e, B, C),
    token_semicolon(C, D).

.decl continue_statement(s: Statement, a: number, b: number)
continue_statement($Continue(id), A, D) :-
    token_continue(A, B),
    identifier?(id, B, C),
    token_semicolon(C, D).

.decl return_statement(s: Statement, a: number, b: number)
return_statement($Return(e), A, D) :-
    token_return(A, B),
    expression?(e, B, C),
    token_semicolon(C, D).

.decl throw_statement(s: Statement, a: number, b: number)
throw_statement($Throw(e), A, D) :-
    token_throw(A, B),
    expression(e, B, C),
    token_semicolon(C, D).

.decl synchronized_statement(s: Statement, a: number, b: number)
synchronized_statement($SynchronizedStatement(e, s), A, F) :-
    token_synchronized(A, B),
    token_left_paren(B, C),
    expression(e, C, D),
    token_right_paren(D, E),
    block(s, E, F).

.decl try_statement(s: Statement, a: number, b: number)

// TODO Catches

.decl catch_clause(e: CatchClause, a: number, b: number)
catch_clause($CatchClause(param, b), A, B) :-
    token_catch(A, B),
    token_left_paren(B, C),
    catch_formal_parameter(param, C, D),
    token_right_paren(D, E),
    block(b, E, F).

.decl catch_formal_parameter(e: CatchFormalParameter, a: number, b: number)

// TODO CatchType

.decl finally(e: Statement, a: number, b: number)
finally(b, A, B) :-
    token_finally(A, B),
    block(b, B, C).

// TODO TryWithResourcesStatement

// TODO ResourceSpecification

// TODO ResourceList

// TODO Resource

/**
 * Expressions
 *
 * Adapted from §15 of
 * https://docs.oracle.com/javase/specs/jls/se15/html/jls-19.html
 **************************************************************************/

.decl primary(e: Expression, a: number, b: number)
primary(e, A, B) :- primary_no_new_array(e, A, B).
primary(e, A, B) :- array_creation_expression(e, A, B).

.decl primary_no_new_array(e: Expression, a: number, b: number)
primary_no_new_array($Literal, A, B) :- literal(A, B).
primary_no_new_array(e, A, B) :- class_literal(e, A, B).
primary_no_new_array($This, A, B) :- token_this(A, B).
primary_no_new_array(e, A, B + 1) :-
    token_left_paren(A, B),
    expression(e, B, C),
    token_right_paren(C, D).
primary_no_new_array(e, A, B) :- class_instance_creation_expression(e, A, B).
primary_no_new_array(e, A, B) :- field_access(e, A, B).
primary_no_new_array(e, A, B) :- array_access(e, A, B).
primary_no_new_array(e, A, B) :- method_invocation(e, A, B).
primary_no_new_array(e, A, B) :- method_reference(e, A, B).

.decl class_literal(e: Expression, a: number, b: number)

.decl class_instance_creation_expression(e: Expression, a: number, b: number)

.decl unqualified_class_instance_creation_expression(e: Expression, a: number, b: number)

.decl class_or_interface_type_to_instantiate(e: Expression, a: number, b: number)

.decl type_arguments_or_diamond(e: Expression, a: number, b: number)

.decl field_access(e: Expression, a: number, b: number)

.decl array_access(e: Expression, a: number, b: number)

.decl method_invocation(e: Expression, a: number, b: number)
method_invocation(e, A, B) :- method_invocation_with_method_name(e, A, B).

.decl method_invocation_with_method_name(e: Expression, a: number, b: number)
method_invocation_with_method_name($MethodInvocation(id, e), A, E) :-
    method_name(id, A, B),
    token_left_paren(B, C),
    argument_list?(e, C, D),
    token_right_paren(D, E).

.decl argument_list?(e: ExpressionList, a: number, b: number)
argument_list?(nil, A, A) :- any_position(A).
argument_list?(e, A, B) :- argument_list(e, A, B).

.decl argument_list(e: ExpressionList, a: number, b: number)
argument_list([x, nil], A, B) :- expression(x, A, B).
argument_list([x, xs], A, D) :-
    expression(x, A, B),
    token_comma(B, C),
    argument_list(xs, C, D).

.decl method_reference(e: Expression, a: number, b: number)

.decl array_creation_expression(e: Expression, a: number, b: number)

.decl dim_exprs(a: number, b: number)
.decl dim_expr(a: number, b: number)

.decl expression(e: Expression, a: number, b: number)
expression(e, A, B) :- conditional_expression(e, A, B).
expression(e, A, B) :- lambda_expression(e, A, B).

.decl expression?(e: ExpressionOptional, a: number, b: number)
OPTIONAL(expression?, expression)

.decl lambda_expression(e: Expression, a: number, b: number)
lambda_expression($Lambda(params, body), A, D) :-
    lambda_parameters(params, A, B),
    token_arrow(B, C),
    lambda_body(body, C, D).

.decl lambda_parameters(p: LambdaParameters, a: number, b: number)

.decl lambda_parameter_list(a: number, b: number)

.decl lambda_parameter(a: number, b: number)

.decl lambda_parameter_type(t: Type, a: number, b: number)
lambda_parameter_type(t, A, B) :- unann_type(t, A, B).
lambda_parameter_type($Var, A, A + 1) :- token_var(A, B).

.decl lambda_body(e: LambdaBody, a: number, b: number)
lambda_body($LambdaBodyExpression(e), A, B) :- expression(e, A, B).
lambda_body($LambdaBodyBlock(e), A, B) :- block(e, A, B).

.decl assignment_expression(e: Expression, a: number, b: number)
assignment_expression(e, A, B) :- conditional_expression(e, A, B).
assignment_expression(e, A, B) :- assignment(e, A, B).

.decl assignment(e: Expression, a: number, b: number)
assignment($Assign(e1, s, e2), A, D) :-
    left_hand_side(e1, A, B),
    assignment_operator(s, B, C),
    expression(e2, C, D).

.decl left_hand_side(e: Expression, a: number, b: number)

.decl assignment_operator(s: symbol, a: number, b: number)
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "==".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "*=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "/=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "%=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "+=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "-=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "<<=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = ">>=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = ">>>=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "&=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "^=".
assignment_operator(s, A, A + 1) :- token(_, s, A), s = "|=".

.decl conditional_expression(e: Expression, a: number, b: number)
conditional_expression(e, A, B) :- conditional_or_expression(e, A, B).
conditional_expression(e, A, D) :-
    conditional_or_expression(e, A, B),
    token_question_mark(B, C),
    expression(e, C, D),
    token_colon(D, E),
    conditional_expression(e, E, F).
conditional_expression(e, A, F) :-
    conditional_or_expression(e, A, B),
    token_question_mark(B, C),
    expression(e, C, D),
    token_colon(D, E),
    lambda_expression(e, E, F).

.decl conditional_or_expression(e: Expression, a: number, b: number)
conditional_or_expression(e, A, B) :- conditional_and_expression(e, A, B).
INFIX($Or, conditional_or_expression, conditional_and_expression, "||")

.decl conditional_and_expression(e: Expression, a: number, b: number)
conditional_and_expression(e, A, B) :- inclusive_or_expression(e, A, B).
INFIX($And, conditional_and_expression, inclusive_or_expression, "&&")

.decl inclusive_or_expression(e: Expression, a: number, b: number)
inclusive_or_expression(e, A, B) :- exclusive_or_expression(e, A, B).
INFIX($BitwiseInclOr, inclusive_or_expression, exclusive_or_expression, "|")

.decl exclusive_or_expression(e: Expression, a: number, b: number)
exclusive_or_expression(e, A, B) :- and_expression(e, A, B).
INFIX($BitwiseExclOr, exclusive_or_expression, and_expression, "^")

.decl and_expression(e: Expression, a: number, b: number)
and_expression(e, A, B) :- equality_expression(e, A, B).
INFIX($BitwiseAnd, and_expression, equality_expression, "&")

.decl equality_expression(e: Expression, a: number, b: number)
equality_expression(e, A, B) :-       relational_expression(e, A, B).
INFIX($Equal,    equality_expression, relational_expression, "==")
INFIX($NotEqual, equality_expression, relational_expression, "!=")

.decl relational_expression(e: Expression, a: number, b: number)
relational_expression(e, A, B) :- shift_expression(e, A, B).

.decl shift_expression(e: Expression, a: number, b: number)
shift_expression(e, A, B) :- additive_expression(e, A, B).
INFIX($ShiftLeft,          shift_expression, additive_expression, "<<")
INFIX($ShiftRight,         shift_expression, additive_expression, ">>")
INFIX($ShiftRightUnsigned, shift_expression, additive_expression, ">>>")

.decl additive_expression(e: Expression, a: number, b: number)
additive_expression(e, A, B) :- multiplicative_expression(e, A, B).
INFIX($Add, additive_expression, multiplicative_expression, "+")
INFIX($Sub, additive_expression, multiplicative_expression, "-")

.decl multiplicative_expression(e: Expression, a: number, b: number)
multiplicative_expression(e, A, B) :- unary_expression(e, A, B).
INFIX($Mul, multiplicative_expression, unary_expression, "*")
INFIX($Div, multiplicative_expression, unary_expression, "/")
INFIX($Mod, multiplicative_expression, unary_expression, "%")

.decl unary_expression(e: Expression, a: number, b: number)
unary_expression(e, A, B) :- postfix_expression(e, A, B).
unary_expression(e, A, B) :- pre_increment_expression(e, A, B).
unary_expression(e, A, B) :- pre_decrement_expression(e, A, B).

.decl pre_increment_expression(e: Expression, a: number, b: number)
PREFIX($PreIncrement, pre_increment_expression, unary_expression, "++")

.decl pre_decrement_expression(e: Expression, a: number, b: number)
PREFIX($PreDecrement, pre_decrement_expression, unary_expression, "--")

.decl unary_expression_not_plus_minus(e: Expression, a: number, b: number)
unary_expression_not_plus_minus(e, A, B) :- postfix_expression(e, A, B).
PREFIX($BitwiseNot, unary_expression_not_plus_minus, unary_expression, "~")
PREFIX($Not, unary_expression_not_plus_minus, unary_expression, "!")

.decl postfix_expression(e: Expression, a: number, b: number)
postfix_expression(e, A, B) :- post_increment_expression(e, A, B).
postfix_expression(e, A, B) :- post_decrement_expression(e, A, B).
postfix_expression($Identifier(id), A, B) :- token_identifier(id, A, B).
postfix_expression($Number(to_number(n)), A, A + 1) :- token("TOKEN_NUMBER", n, A).

.decl post_increment_expression(e: Expression, a: number, b: number)
POSTFIX($PostIncrement, post_increment_expression, postfix_expression, "++")
.decl post_decrement_expression(e: Expression, a: number, b: number)
POSTFIX($PostDecrement, post_decrement_expression, postfix_expression, "--")

.decl cast_expression(e: Expression, a: number, b: number)

.decl switch_expression(e: Expression, a: number, b: number)
switch_expression($SwitchExpr(e1, rules), A, F) :-
    token_switch(A, B),
    token_left_paren(B, C),
    expression(e1, C, D),
    token_right_paren(D, E),
    switch_block(rules, E, F).

.decl constant_expression(e: Expression, a: number, b: number)
constant_expression(e, A, B) :- expression(e, A, B).

/**
 * Root
 **************************************************************************/

.decl root(s: Statement)
root(s) :-
    statement(s, 1, N),
    token("TOKEN_EOF", _, N + 1).

.output expression
.output return_statement
.output statement
.output class_modifiers?
.output type_identifier
.output class_body
.output continue_statement
.output method_invocation
.output normal_class_declaration
.output argument_list?
